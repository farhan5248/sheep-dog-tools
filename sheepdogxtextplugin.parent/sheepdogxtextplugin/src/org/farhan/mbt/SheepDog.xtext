// TODO try using the built in terminals instead and override them
grammar org.farhan.mbt.SheepDog hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sheepDog "http://www.farhan.org/mbt/SheepDog"

Model:
	StepObject | Feature;

StepObject:
	'=' 'Object:' name=Title EOL
	statements+=Statement*
	stepDefinitions+=StepDefinition*;

StepDefinition:
	'==' 'Definition:' name=Title EOL
	statements+=Statement*
	stepParameters+=StepParameters*;

StepParameters:
	'===' 'Parameters:' name=Title EOL
	statements+=Statement*
	parametersTable=Table;

Feature:
	(tags=FeatureTags)?
	'=' 'Feature:' name=Title EOL
	statements+=Statement*
	abstractScenarios+=AbstractScenario*;

AbstractScenario:
	Background | Scenario;

Background:
	(tags=AbstractScenarioTags)?
	'==' 'Background:' name=Title EOL
	statements+=Statement*
	steps+=Step*;

Scenario:
	(tags=AbstractScenarioTags)?
	'==' 'Scenario:' name=Title EOL
	statements+=Statement*
	steps+=Step*
	examples+=Examples*;

// TODO this is confusing, examples has scenario tags? rename it
Examples:
	(tags=AbstractScenarioTags)?
	'===' 'Examples:' name=Title EOL
	statements+=Statement*
	theExamplesTable=Table;

Step:
	Given | When | Then | And;

Given:
	'===' 'Given:' name=Title EOL
	(theStepTable=Table | theDocString=DocString)?;

When:
	'===' 'When:' name=Title EOL
	(theStepTable=Table | theDocString=DocString)?;

Then:
	'===' 'Then:' name=Title EOL
	(theStepTable=Table | theDocString=DocString)?;

And:
	'===' 'And:' name=Title EOL
	(theStepTable=Table | theDocString=DocString)?;

DocString:
	name=RAWTEXT EOL;

Table:
	'|===' EOL
	rows+=Row+
	'|===' EOL;

Row:
	cells+=Cell+ EOL;

FeatureTags:
	':tags:' name=WORD EOL;

AbstractScenarioTags:
	'[tags=' name=WORD ']' EOL;

Cell:
	'|' name=Title;

Statement:
	name=Title EOL;

// TODO maybe I don't need Title and can ignore the warnings about it being used in a loop
Title:
	WORD+;

// TODO look at fragments to handle escape characters etc instead of this and to handle re-using ' ' | '\t' | '\r' | '\n'
terminal WORD:
	!(' ' | '\t' | '\r' | '\n' | '[' | ']' | '|' | '#')
	!(' ' | '\t' | '\r' | '\n' | '[' | ']')*;

terminal WS:
	(' ' | '\t' | '\r')+;

terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;

terminal RAWTEXT:
	'----' '\n' (.)+ '\n' '----';
