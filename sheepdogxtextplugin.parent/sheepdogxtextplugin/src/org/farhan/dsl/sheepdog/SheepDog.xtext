// TODO try using the built in terminals instead and override them
grammar org.farhan.dsl.sheepdog.SheepDog hidden(WS, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sheepDog "http://www.farhan.org/dsl/sheepdog"

Model:
	StepObject | TestSuite;

StepObject:
	'=' 'Step-Object:' name=Title EOL
	statementList+=Statement*
	stepDefinitionList+=StepDefinition*;

StepDefinition:
	'==' 'Step-Definition:' name=Title EOL
	statementList+=Statement*
	stepParameterList+=StepParameters*;

StepParameters:
	'*' 'Step-Parameters:' name=Title EOL
	statementList=StatementList?
	table=Table;

TestSuite:
	'=' 'Test-Suite:' name=Title EOL
	statementList+=Statement*
	testStepContainerList+=TestStepContainer*;

TestStepContainer:
	TestSetup | TestCase;

TestSetup:
	'==' 'Test-Setup:' name=Title EOL
	statementList+=Statement*
	testStepList+=TestStep*;

TestCase:
	'==' 'Test-Case:' name=Title EOL
	statementList+=Statement*
	testStepList+=TestStep*
	testDataList+=TestData*;

TestData:
	'*' 'Test-Data:' name=Title EOL
	statementList=StatementList?
	table=Table;

TestStep:
	Given | When | Then | And;

Given:
	'*' 'Given:' name=Title EOL
	(table=Table | text=Text)?;

When:
	'*' 'When:' name=Title EOL
	(table=Table | text=Text)?;

Then:
	'*' 'Then:' name=Title EOL
	(table=Table | text=Text)?;

And:
	'*' 'And:' name=Title EOL
	(table=Table | text=Text)?;

Text:
	'+' EOL
	name=RAWTEXT EOL;

StatementList:
	'+' EOL
	statementList+=Statement+;

Table:
	'+' EOL
	'|===' EOL
	rowList+=Row+
	'|===' EOL;

Row:
	cellList+=Cell+ EOL;

Cell:
	'|' name=Title;

Statement:
	name=Title EOL;

// TODO maybe I don't need Title and can ignore the warnings about it being used in a loop
Title:
	WORD+;

// TODO look at fragments to handle escape characters etc instead of this and to handle re-using ' ' | '\t' | '\r' | '\n'
terminal WORD:
	!(' ' | '\t' | '\r' | '\n' | '[' | ']' | '|' | '#')
	!(' ' | '\t' | '\r' | '\n' | '[' | ']')*;

terminal WS:
	(' ' | '\t' | '\r')+;

terminal SL_COMMENT:
	'#' !('\n')* EOL;

terminal EOL:
	('\n')+;

terminal RAWTEXT:
	'----' '\n' (.)+ '\n' '----';
